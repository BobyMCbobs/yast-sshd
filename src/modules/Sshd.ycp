/****************************************************************************

Copyright (c) 2000 - 2012 Novell, Inc.
All Rights Reserved.

This program is free software; you can redistribute it and/or
modify it under the terms of version 2 of the GNU General Public License as
published by the Free Software Foundation.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.   See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, contact Novell, Inc.

To contact Novell about this file by physical or electronic mail,
you may find current contact information at www.novell.com

****************************************************************************/
/**
 * File:	modules/Sshd.ycp
 * Package:	Configuration of SSHD
 * Summary:	SSHD settings, input and output functions
 * Authors:	Lukas Ocilka <locilka@suse.cz>
 *
 * Representation of the configuration of SSHD.
 * Input and output routines.
 */

{

module "Sshd";
textdomain "sshd";

import "Progress";
import "Report";
import "Message";
import "Service";
import "Popup";
import "SCR";
import "Mode";
import "SuSEFirewall";

/**
 * Data was modified?
 */
boolean modified = false;

/**
 * Returns whether the configuration has been modified.
 */
global boolean GetModified() {
    return modified;
}

/**
 * Sets that the configuration has been modified.
 */
global void SetModified() {
    modified = true;
}

string service_name = "sshd";

boolean start_service = nil;

global boolean GetStartService () {
    return start_service;
}

global void SetStartService (boolean new_state) {
    if (new_state == nil) {
	y2error ("Cannot set 'StartService' to %1", new_state);
	return;
    } else if (start_service == new_state) {
	y2warning ("'StartService' unchanged");
	return;
    }

    start_service = new_state;
    SetModified();
}

/**
 * Describes whether the daemon is running
 */
boolean Running () {
    return (Service::Status(service_name) == 0);
}

boolean ReadStartService () {
    start_service = Running();

    return (start_service != nil);
}

/**
* map of SSHD settings
*/
    map <string, list<string> > SETTINGS = $[];

    // FIXME: read the default config from configuration file
    map <string, list<string> > DEFAULT_CONFIG = $[
	"Port"				: ["22"],
	"AllowTcpForwarding"		: ["yes"],
	"X11Forwarding"			: ["no"],
	"Compression"			: ["yes"],
	"PrintMotd"			: ["yes"],
	"PermitRootLogin"		: ["yes"],
	"IgnoreUserKnownHosts"		: ["no"],
	"MaxAuthTries"			: ["6"],
	// BNC #469207
	// "PasswordAuthentication"	: ["yes"],
	"RSAAuthentication"		: ["no"],
	"PubkeyAuthentication"		: ["yes"],
	"Ciphers"			: ["aes128-cbc,3des-cbc,blowfish-cbc,cast128-cbc,arcfour128,arcfour256,arcfour,aes192-cbc,aes256-cbc,aes128-ctr,aes192-ctr,aes256-ctr"],
    ];

/**
 * Reads current sshd configuration
 */
boolean ReadSSHDSettings () {
    foreach (string key, (list <string>) SCR::Dir(.etc.ssh.sshd_config), {
	list <string> val = (list <string>) SCR::Read(add(.etc.ssh.sshd_config, key));
        if (val != nil) SETTINGS[key] = val;
    });

    y2milestone("SSHD configuration has been read: %1", SETTINGS);
    return true;
}

/**
 * Writes current sshd configuration
 */
boolean WriteSSHDSettings () {
    y2milestone("Writing SSHD configuration: %1", SETTINGS);

    foreach (string option_key, list <string> option_val, SETTINGS, {
	SCR::Write(add(.etc.ssh.sshd_config, option_key), option_val);
    });
    // This is very important
    // it flushes the cache, and stores the configuration on the disk
    SCR::Write(.etc.ssh.sshd_config, nil);

    return true;
}

/**
 * Returns the SSHD Option as a list of strings.
 *
 * @param string option_key of the SSHD configuration
 * @return list <string> with option_values
 */
global list <string> GetSSHDOption (string option_key) {
    return SETTINGS[option_key]:DEFAULT_CONFIG[option_key]:[];
}

/**
 * Returns default SSHD Option as a list of strings.
 *
 * @param string option_key of the SSHD configuration
 * @return list <string> with option_values
 */

global list <string> GetDefaultSSHDOption (string option_key) {
	return DEFAULT_CONFIG[option_key]:[];
}

/**
 * Sets values for an option.
 *
 * @param string option_key with the SSHD configuration key
 * @param list <string> option_values with the SSHD configuration values
 */
global void SetSSHDOption (string option_key, list <string> option_vals) {
    SETTINGS[option_key] = option_vals;
}

/**
 * Exports the current configuration.
 *
 * @return map of a current configuration
 *
 * @struct $[
 *   "config" : (map <string, list<string> >) SETTINGS,
 *   "status" : (boolean) service_status,
 * ]
 */
global map Export () {
    return $[
	"config" : SETTINGS,
	"status" : Running(),
    ];
}

/**
 * Imports a configuration
 */
global boolean Import (map import_map) {
    SETTINGS = (map <string, list<string> >) import_map["config"]:$[];
    SetStartService (import_map["status"]:false == true);
    SetModified();
    y2milestone ("Configuration has been imported");
    return true;
}

/**
  Returns a confirmation popup dialog whether user wants to really abort.
 */
global boolean Abort() {
    return Popup::ReallyAbort(GetModified());
}

/**
 * Checks whether an Abort button has been pressed.
 * If so, calls function to confirm the abort call.
 *
 * @return boolean true if abort confirmed
 */
global boolean PollAbort() {
    // Do not check UI when running in CommandLine mode
    if (Mode::commandline())
	return false;

    if (UI::PollInput() == `abort)
	return Abort();

    return false;
}

/**
 * Read all SSHD settings
 * @return true on success
 */
global boolean Read() {

    /* SSHD read dialog caption */
    string caption = _("Initializing the SSHD Configuration");

    integer steps = 4;

    Progress::New( caption, " ", steps, [
	    /* Progress stage 1 */
	    _("Read the current SSHD configuration"),
	    /* Progress stage 2 */
	    _("Read the current SSHD state"),
	    /* Progress stage 3 */
	    _("Read firewall settings"),
	    /* Progress stage 4 */
	    _("Read service status"),
	], [
	    /* Progress step 1 */
	    _("Reading the current SSHD configuration..."),
	    /* Progress step 2 */
	    _("Reading the current SSHD state"),
	    /* Progress step 3 */
	    _("Reading firewall settings..."),
	    /* Progress step 4 */
	    _("Reading service status..."),
	    Message::Finished()
	],
	""
    );

    if (PollAbort()) return false;
    Progress::NextStage();
    if(!ReadSSHDSettings()) Report::Error(Message::CannotReadCurrentSettings());

    if (PollAbort()) return false;
    Progress::NextStage();
    if (!ReadStartService()) Report::Error(Message::CannotReadCurrentSettings());

    if (PollAbort()) return false;
    Progress::NextStep();
    boolean progress_state = Progress::set (false);
    /* Error message */
    if(!SuSEFirewall::Read()) Report::Error(_("Cannot read firewall settings."));
    Progress::set (progress_state);

    if (PollAbort()) return false;
    Progress::NextStage ();

    if (PollAbort()) return false;
    Progress::NextStage ();
    start_service = Service::Enabled (service_name);

    modified = false;
    return true;
}

boolean AdjustSSHDService () {
    boolean enable_and_start = GetStartService();
    boolean enabled = Service::Enabled (service_name);
    boolean running = (Service::Status (service_name) == 0);

    if (enable_and_start == nil) {
	y2error ("Configuration error: Cannot start/stop service %1", service_name);
	return false;
    }

    // Service enable/disable
    if (enable_and_start == enabled) {
	y2milestone ("Service '%1' is already in the desired state", service_name);
    } else {
	if (enable_and_start && !Service::Enable (service_name)) {
	    y2error ("Cannot enable service %1", service_name);
	    return false;
	} else if (!enable_and_start && !Service::Disable (service_name)) {
	    y2error ("Cannot disable service %1", service_name);
	    return false;
	}
    }

    // Start / restart service
    if (enable_and_start) {
	if (Running()) {
	    if (!Service::Restart(service_name)) {
		y2error ("Cannot restart service %1", service_name);
		return false;
	    }
	} else {
	    if (!Service::Start (service_name)) {
		y2error ("Cannot start service %1", service_name);
		return false;
	    }
	}
    // Stop a running service
    } else if (Running()) {
	if (!Service::Stop (service_name)) {
	    return false;
	}
    }

    return true;
}

/**
 * Write all SSHD settings
 * @return true on success
 */
global boolean Write() {

    /* SSHD read dialog caption */
    string caption = _("Saving the SSHD Configuration");

    integer steps = 4;

    Progress::New(caption, " ", steps, [
	    /* Progress stage 1 */
	    _("Write the SSHD settings"),
	    /* Progress stage 2 */
	    _("Write firewall settings"),
	    /* Progress stage 3 */
	    _("Adjust SSHD service"),
	], [
	    /* Progress step 1 */
	    _("Writing the SSHD settings..."),
	    /* Progress step 2 */
	    _("Writing firewall settings..."),
	    /* Progress step 3 */
	    _("Adjusting SSHD service..."),
	    Message::Finished()
	],
	""
    );

    if(PollAbort()) return false;
    Progress::NextStage();
    /* Error message */
    if(!WriteSSHDSettings()) Report::Error (_("Cannot write the SSHD settings."));

    if(PollAbort()) return false;
    Progress::NextStage ();
    boolean progress_state = Progress::set (false);
    /* Error message */
    if(!SuSEFirewall::Write()) Report::Error(_("Cannot write firewall settings."));
    Progress::set (progress_state);

    if(PollAbort()) return false;
    Progress::NextStage ();
    AdjustSSHDService();

    Progress::NextStage ();

    return true;
}

/* AutoYaST-related functions */

global string Summary () {
    string ret = "";
    
    // TRANSLATORS: summary item
    if (! Sshd::GetModified()) {
	// TRANSLATORS: summary item
	return _("Not configured yet.");
    }

    list <string> ports = Sshd::GetSSHDOption("Port");
    if (size(ports) == 1) {
	// TRANSLATORS: summary item
	ret = ret + sformat(_("SSHD Server will use port: %1"), ports[0]:"");
    } else if (size(ports) > 0) {
	// TRANSLATORS: summary item
	ret = ret + sformat(_("SSHD Server will use ports: %1"), mergestring(ports, ", "));
    }

    return ret;
}

/* AutoYaST-related functions */

}
