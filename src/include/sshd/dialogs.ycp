/****************************************************************************

Copyright (c) 2000 - 2012 Novell, Inc.
All Rights Reserved.

This program is free software; you can redistribute it and/or
modify it under the terms of version 2 of the GNU General Public License as
published by the Free Software Foundation.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.   See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, contact Novell, Inc.

To contact Novell about this file by physical or electronic mail,
you may find current contact information at www.novell.com

****************************************************************************/
/**
 * File:	include/sshd/dialogs.ycp
 * Package:	Configuration of sshd
 * Summary:	Dialogs definitions
 * Authors:	Lukas Ocilka <locilka@suse.cz>
 *
 * $Id: dialogs.ycp 13879 2004-02-05 11:29:30Z jtf $
 */

{

textdomain "sshd";

import "Label";
import "Wizard";
import "Sshd";

include "sshd/helps.ycp";

map current_ui_settings = UI::GetDisplayInfo();

integer mbox_x = 1;
integer mbox_y = 0.5;

// Tabs have a different layout in textmode
if (current_ui_settings["TextMode"]:false == true) {
    mbox_x = 0;
    mbox_y = 0;
}

term ServiceConfigurationDialogContent () {
    return `VBox (
	"start_stop",
	`VSpacing(1),
	"auto_start_up",
	`VStretch()
    );
}

term ServerConfigurationDialogContent () {
    return `VBox (
	`Left(`Label(_("SSHD TCP Ports"))),
	`Left(
	    `VBox (
		`MinSize( 40, 5,
		    /* A table header */
		    `Table(`id("Port"), `header(_("Port")), [])
		),
		`Left (`HBox (
		    /* a push button */
		    `PushButton(`id("add_port"), _("&Add...")),
		    /* a push button */
		    `PushButton(`id("edit_port"), _("&Edit...")),
		    /* a push button */
		    `PushButton(`id("delete_port"), _("&Delete"))
		)),
		`VSpacing (1),
		`Frame (
		    /* a dialog frame caption */
		    _("Server Features"),
		    `MarginBox (mbox_x, mbox_y, `VBox (
			/* a check box */
			`Left(`CheckBox(`id("AllowTcpForwarding"), _("Allow &TCP Forwarding"))),
			/* a check box */
			`Left(`CheckBox(`id("X11Forwarding"),      _("Allow &X11 Forwarding"))),
			/* a check box */
			`Left(`CheckBox(`id("Compression"),        _("Allow &Compression")))
		    ))
		),
		`VSpacing(1),
		"fw",
		`VStretch()
	    )
	)
    );
}

term LoginSettingsDialogContent () {
    return `VBox (
	`Frame (
	    _("General Login Settings"),
	    `MarginBox (mbox_x, mbox_y, `VBox (
		/* A check box */
		`Left(`CheckBox(`id("PrintMotd"),            _("Print &Message of the Day after Login"))),
		/* A check box */
		`Left(`CheckBox(`id("PermitRootLogin"),      _("Permi&t Root Login")))
	    ))
	),
	`VSpacing(1),
	`Frame (
	    _("Authentication Settings"),
	    `MarginBox (mbox_x, mbox_y, `VBox (
		/* A text entry */
		`Left(`InputField(`id("MaxAuthTries"),           _("Ma&ximum Authentication Tries"))),

// BNC #469207
//		/* A check box */
//		`Left(`CheckBox (`id("PasswordAuthentication"), _("Pa&ssword Authentication"))),

		/* A check box */
		`Left(`CheckBox (`id("RSAAuthentication"),      _("RSA Authenti&cation (Protocol V. 1 Only)"))),
		/* A check box */
		`Left(`CheckBox (`id("PubkeyAuthentication"),   _("Public &Key Authentication (Protocol V. 2 Only)")))
	    ))
	),
	`VStretch()
    );
}

term ProtoAndCipherDialogContent () {
	return `VBox (
		`Frame(
			_("Supported SSH protocol versions"),
			`RadioButtonGroup (`id(`rb),
				`MarginBox (mbox_x, mbox_y, `VBox(
					`Left(`RadioButton (`id("SSHv21"), _("&2 and 1"))),
					`Left(`RadioButton (`id("SSHv2"), _("2 &only"))),
					`Left(`RadioButton (`id("SSHv1"), _("&1 only")))
				))
			)
		),
		`HBox(
			`VBox(
				`Left(`ComboBox(`id("supported_ciphers"), _("&Supported Ciphers"),[])),
			     `Table(`id("Ciphers"), `header(_("Cipher")), [])
			),
			`HSquash( 
				`VBox (
				    `VSpacing( 1.1 ),
					`PushButton ( `id ( "add_cipher" ), `opt ( `hstretch ), " " + Label::AddButton() + " "),
				    `PushButton ( `id ( "remove_cipher" ), `opt ( `hstretch ), " " + Label::DeleteButton() + " "),
				    `Empty(`opt(`vstretch))

				)
			)
		),
		`VStretch()
	);
}

}
