/**
 * File:	include/sshd/dialogs.ycp
 * Package:	Configuration of sshd
 * Summary:	Dialogs definitions
 * Authors:	Lukas Ocilka <locilka@suse.cz>
 *
 * $Id: dialogs.ycp 13879 2004-02-05 11:29:30Z jtf $
 */

{

textdomain "sshd";

import "Label";
import "Wizard";
import "Sshd";

include "sshd/helps.ycp";

map current_ui_settings = UI::GetDisplayInfo();

integer mbox_x = 1;
integer mbox_y = 0.5;

// Tabs have a different layout in textmode
if (current_ui_settings["TextMode"]:false == true) {
    mbox_x = 0;
    mbox_y = 0;
}

term ServerConfigurationDialogContent () {
    return `MarginBox (mbox_x, mbox_y, `VBox (
	`Left(`Label(_("SSHD TCP Ports"))),
	`Left(
	    `VBox (
		`MinSize( 40, 5,
		    /* A table header */
		    `Table(`id("Port"), `header(_("Port")), [])
		),
		`Left (`HBox (
		    /* a push button */
		    `PushButton(`id("add_port"), _("&Add...")),
		    /* a push button */
		    `PushButton(`id("edit_port"), _("&Edit...")),
		    /* a push button */
		    `PushButton(`id("delete_port"), _("&Delete"))
		)),
		`VSpacing(1),
		`MarginBox (mbox_x, mbox_y, `Frame (
		    /* a dialog frame caption */
		    _("Server Features"),
		    `VBox (
			/* a check box */
			`Left(`CheckBox(`id("AllowTcpForwarding"), _("Allow &TCP Forwarding"))),
			/* a check box */
			`Left(`CheckBox(`id("X11Forwarding"),      _("Allow &X11 Forwarding"))),
			/* a check box */
			`Left(`CheckBox(`id("Compression"),        _("Allow &Compression")))
		    )
		)),
		`VStretch()
	    )
	)
    ));
}

term LoginSettingsDialogContent () {
    return `MarginBox (mbox_x, mbox_y, `VBox(
	`Frame (
	    _("General Login Settings"),
	    `VBox (
		/* A check box */
		`Left(`CheckBox(`id("PrintMotd"),            _("Print &Message of the Day After Login"))),
		/* A check box */
		`Left(`CheckBox(`id("PermitRootLogin"),      _("Permi&t Root Login")))
	    )
	),
	`VSpacing(1),
	`Frame (
	    _("Authentication Settings"),
	    `VBox (
		/* A text entry */
		`Left(`InputField(`id("MaxAuthTries"),           _("Ma&ximum Authentication Tries"))),
		/* A check box */
		`Left(`CheckBox (`id("PasswordAuthentication"), _("Pa&ssword Authentication"))),
		/* A check box */
		`Left(`CheckBox (`id("RSAAuthentication"),      _("RSA Authenti&cation"))),
		/* A check box */
		`Left(`CheckBox (`id("PubkeyAuthentication"),   _("Public &Key Authentication")))
	    )
	),
	`VStretch()
    ));
}

term ProtoAndCipherDialogContent () {
	return `MarginBox (mbox_x, mbox_y, `VBox (
		`Frame(
			_("Supported SSH protocol versions"),
			`RadioButtonGroup (`id(`rb),
				`VBox(
					`Left(`RadioButton (`id("SSHv21"), _("&2 and 1"))),
					`Left(`RadioButton (`id("SSHv2"), _("2 &only"))),
					`Left(`RadioButton (`id("SSHv1"), _("&1 only")))
				)
			)
		),
		`HBox(
			`VBox(
				`Left(`ComboBox(`id("supported_ciphers"), _("&Supported Ciphers"),[])),
			     `Table(`id("Ciphers"), `header("Cipher"), [])
			),
			`HSquash( 
				`VBox (
				    `VSpacing( 1.1 ),
					`PushButton ( `id ( "add_cipher" ), `opt ( `hstretch ), " " + Label::AddButton() + " "),
				    `PushButton ( `id ( "remove_cipher" ), `opt ( `hstretch ), " " + Label::RemoveButton() + " "),
				    `Empty(`opt(`vstretch))

				)
			)
		),
		`VStretch()
	));
}

}
